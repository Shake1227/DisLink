plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}
group = 'shake_1227.dislink'
version = '1.0'
description = 'A Discord link plugin for Minecraft servers'
java.sourceCompatibility = JavaVersion.VERSION_17
repositories {
    mavenCentral()
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://m2.dv8tion.net/releases' }
}
dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.20.1-R0.1-SNAPSHOT'
    implementation 'net.dv8tion:JDA:5.0.0-beta.14'
    implementation 'com.squareup.okhttp3:okhttp:4.11.0'
    implementation 'com.squareup.okio:okio:3.5.0'
    compileOnly 'org.jetbrains:annotations:24.0.1'
    testImplementation 'junit:junit:4.13.2'
}
tasks {
    build {
        dependsOn shadowJar
    }
    shadowJar {
        archiveClassifier.set('')
        archiveFileName.set("DisLink-${version}.jar")
        mergeServiceFiles()
        relocate 'net.dv8tion', 'shake_1227.shaded.net.dv8tion'
        relocate 'okhttp3', 'shake_1227.shaded.okhttp3'
        relocate 'okio', 'shake_1227.shaded.okio'
    }
}
java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}
configurations.all {
    resolutionStrategy.cacheChangingModulesFor(0, 'seconds')
}
processResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
    }
}
